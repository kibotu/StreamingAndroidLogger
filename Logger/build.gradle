apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

android {

    compileSdk compileSdkVer
    buildToolsVersion = buildToolsVer

    namespace "net.kibotu.logger"

    defaultConfig {

        minSdk minSdkVer
        targetSdk targetSdkVer

        versionCode VERSION_CODE
        versionName VERSION_NAME

        resConfigs "en"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        release {
            pseudoLocalesEnabled false
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // region java compile options

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    // endregion

    // region kotlin compile options

    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs += [
                // disable linting for opt-in annotations
                "-Xopt-in=kotlin.RequiresOptIn",
        ]
    }

    // endregion

    // region lint options

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false        // true by default
        checkAllWarnings false
        checkReleaseBuilds false
        ignoreWarnings true       // false by default
        quiet true                // false by default
    }

    // endregion

    // region adb options

    adbOptions {
        timeOutInMs 10 * 60 * 1000 // 10 minutes
    }

    // endregion
}


dependencies {
    implementation libs.appCompat
    implementation libs.androidKtxCore
    implementation libs.material
    implementation libs.gson
    compileOnly(libs.crashlytics) { transitive = true }
    compileOnly libs.applicationProvider
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "com.github.kibotu"
                artifactId = 'StreamingAndroidLogger'
                version = VERSION_NAME
            }
        }
    }
}
